project.ext {
    compileSdk = 30
    applicationId = 'com.zyh.componentization'
    minSdk = 21
    targetSdk = 30
    versionCode = 1
    versionName = '1.0'

    isModule = false

    //设置app配置
    setAppDefaultConfig = {
        extension ->
            extension.apply plugin: 'com.android.application'
            extension.apply plugin: 'kotlin-android'
            extension.apply plugin: 'kotlin-android-extensions'
            extension.apply plugin: 'kotlin-kapt'
            extension.description "app"
            projectName = "app"
            //设置通用Android配置
            setAppAndroidConfig extension.android
            //设置通用依赖配置
            setDependencies extension.dependencies

    }

    //设置app android配置
    setAppAndroidConfig = {
        extension -> //extension 相当于 android 对象
            extension.compileSdk project.ext.compileSdk
            extension.defaultConfig {
                applicationId project.ext.applicationId
                minSdk project.ext.minSdk
                targetSdk project.ext.targetSdk
                versionCode project.ext.versionCode
                versionName project.ext.versionName

                testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

                //ARouter 编译生成路由
                javaCompileOptions {
                    annotationProcessorOptions {
                        arguments = [AROUTER_MODULE_NAME: project.getName()]
                    }
                }
            }

            extension.lintOptions {
                checkReleaseBuilds false
                abortOnError false
            }

            extension.resourcePrefix "${project.name}_"

            extension.compileOptions {
                sourceCompatibility JavaVersion.VERSION_1_8
                targetCompatibility JavaVersion.VERSION_1_8
            }

            extension.kotlinOptions {
                jvmTarget = '1.8'
            }
    }

    //设置lib配置（可作为project单独调试）
    setAppOrLibDefaultConfig = {
        extension ->
            if (project.ext.isModule) {
                extension.apply plugin: 'com.android.application'
                extension.description "app"
            } else {
                extension.apply plugin: 'com.android.library'
                extension.description "lib"
            }

            extension.apply plugin: 'kotlin-android'
            extension.apply plugin: 'kotlin-android-extensions'
            extension.apply plugin: 'kotlin-kapt'

            projectName = project.name
            //设置通用Android配置
            setAppOrLibAndroidConfig extension.android
            //设置通用依赖配置
            setDependencies extension.dependencies
    }

    //设置lib android配置（可作为project单独调试）
    setAppOrLibAndroidConfig = {
        extension -> //extension 相当于 android 对象
            extension.compileSdk project.ext.compileSdk
            extension.defaultConfig {
                minSdk project.ext.minSdk
                targetSdk project.ext.targetSdk
                versionCode project.ext.versionCode
                versionName project.ext.versionName

                testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
                consumerProguardFiles 'consumer-rules.pro'

                //ARouter 编译生成路由
                javaCompileOptions {
                    annotationProcessorOptions {
                        arguments = [AROUTER_MODULE_NAME: project.getName()]
                    }
                }
            }

            extension.resourcePrefix "${project.name}_"

            if (project.ext.isModule) {
                extension.compileOptions {
                    sourceCompatibility JavaVersion.VERSION_1_8
                    targetCompatibility JavaVersion.VERSION_1_8
                }

                extension.kotlinOptions {
                    jvmTarget = '1.8'
                }
            }

            extension.sourceSets {
                main {
                    if (project.ext.isModule) {
                        manifest.srcFile 'src/main/manifest/AndroidManifest.xml'
                    } else {
                        manifest.srcFile 'src/main/AndroidManifest.xml'
                    }
                }
            }
    }

    //设置lib配置（只可以作为lib,不可单独调试）
    setLibDefaultConfig = {
        extension ->
            extension.apply plugin: 'com.android.library'
            extension.apply plugin: 'kotlin-android'
            extension.apply plugin: 'kotlin-kapt'
            extension.description "lib"

            projectName = project.name
            //设置通用Android配置
            setLibAndroidConfig extension.android
            //设置通用依赖配置
            setDependencies extension.dependencies
    }

    //设置lib android配置（只可以作为lib,不可单独调试）
    setLibAndroidConfig = {
        extension -> //extension 相当于 android 对象
            extension.compileSdk project.ext.compileSdk
            extension.defaultConfig {
                minSdk project.ext.minSdk
                targetSdk project.ext.targetSdk
                versionCode project.ext.versionCode
                versionName project.ext.versionName

                testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
                consumerProguardFiles 'consumer-rules.pro'

                //ARouter 编译生成路由
                javaCompileOptions {
                    annotationProcessorOptions {
                        arguments = [AROUTER_MODULE_NAME: project.getName()]
                    }
                }
            }

            extension.resourcePrefix "${project.name}_"

            extension.compileOptions {
                sourceCompatibility JavaVersion.VERSION_1_8
                targetCompatibility JavaVersion.VERSION_1_8
            }

            extension.kotlinOptions {
                jvmTarget = '1.8'
            }
    }

    //设置依赖
    setDependencies = {
        extension ->
            extension.implementation fileTree(dir: 'libs', include: ['*.jar'])
            extension.implementation 'androidx.core:core-ktx:1.3.1'
            extension.implementation 'androidx.appcompat:appcompat:1.3.1'
            extension.implementation 'com.google.android.material:material:1.4.0'
            extension.implementation 'androidx.constraintlayout:constraintlayout:2.0.1'
            //协程
            extension.implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.2"
            //auto-service
            extension.implementation 'com.google.auto.service:auto-service-annotations:1.0.1'
            extension.kapt 'com.google.auto.service:auto-service:1.0.1'
            //ARouter 路由apt插件，用于生成相应代码，每个module都需要
            if (projectName[0..2] != 'lib') {
                extension.kapt 'com.alibaba:arouter-compiler:1.5.2'
            }
    }
}